Перем КаталогСПриложениями;

Процедура ПриИнициализацииПоделки(Поделка) Экспорт

	ДобавитьЖелудиСервера(Поделка);
	ДобавитьЖелудиПриложений(Поделка);
	
КонецПроцедуры

Процедура ДобавитьЖелудиПриложений(Поделка)
	
	Файлы = НайтиФайлы(КаталогСПриложениями, "*.os", Истина);
	Для Каждого Файл Из Файлы Цикл
		ПодключитьСценарий(Файл.ПолноеИмя, Файл.ИмяБезРасширения);	
	КонецЦикла;

	Поделка.ПросканироватьКаталог(КаталогСПриложениями);

КонецПроцедуры

Процедура ДобавитьЖелудиСервера(Поделка)
	КаталогМодуля = ТекущийСценарий().Каталог;
	Файлы = НайтиФайлы(КаталогМодуля, "*.os", Истина);
	Для Каждого Файл Из Файлы Цикл
		ТипЖелудя = Неопределено;
		Попытка
			ТипЖелудя = Тип(Файл.ИмяБезРасширения);
		Исключение
			Продолжить;
		КонецПопытки;

		РефлекторОбъекта = Новый РефлекторОбъекта(ТипЖелудя);
		Если РефлекторОбъекта.ПолучитьТаблицуМетодов("Желудь", Ложь).Количество() > 0 Тогда
			Поделка.ДобавитьЖелудь(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Дуб", Ложь).Количество() > 0 Тогда
			Поделка.ДобавитьДуб(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Напильник", Ложь).Количество() > 0 Тогда
			Поделка.ДобавитьНапильник(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Рогатка", Ложь).Количество() > 0 Тогда
			Поделка.ДобавитьРогатку(ТипЖелудя);
		Иначе // BSLLS:EmptyCodeBlock-off
			// no-op
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&Заготовка
Процедура ПриСозданииОбъекта(&Деталька("winow.КаталогСПриложениями") _КаталогСПриложениями)
	КаталогСПриложениями = _КаталогСПриложениями;	
КонецПроцедуры