
&Желудь
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Функция СформироватьПараметры(Действие, ВходящийЗапрос, Ответ, Сессия) Экспорт

	ПараметрыМаршрута = Действие.Параметры;

	Результат = Новый Массив();

	ЗначенияПараметров = СтруктураЗначенийПараметровДляТочкиМаршрута(ВходящийЗапрос, Ответ, Сессия);

	Если Действие.Свойство("ПараметрыУРЛ") Тогда
		ДополнитьЗначениямиПараметровУРЛ(Действие.ПараметрыУРЛ, ВходящийЗапрос.ПараметрыПорядковые, ЗначенияПараметров);
	КонецЕсли;

	Для Каждого ПараметрМаршрута из ПараметрыМаршрута Цикл
		Значение = Неопределено;
		Если Не ЗначенияПараметров.Свойство(ПараметрМаршрута, Значение) Тогда
			ВызватьИсключение СтрШаблон("Неизвестный параметр <%1> в точке маршрута", ПараметрМаршрута);
		КонецЕсли;
		Результат.Добавить(Значение);
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

Процедура ДополнитьЗначениямиПараметровУРЛ(ПараметрыУРЛ, ПараметрыПорядковые, ЗначенияПараметров)
	Счетчик = 0;
	Для каждого ТекПараметрУРЛ Из ПараметрыУРЛ Цикл
		Если Счетчик <= ПараметрыПорядковые.ВГраница() Тогда
			ЗначенияПараметров.Вставить(ТекПараметрУРЛ, ПараметрыПорядковые[Счетчик]);
		Иначе
			ЗначенияПараметров.Вставить(ТекПараметрУРЛ);
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
КонецПроцедуры

Функция СтруктураЗначенийПараметровДляТочкиМаршрута(ВходящийЗапрос, Ответ, Сессия)
	Значения = Новый Структура();	
	ДополнитьСтруктуру(Значения, ВходящийЗапрос.ЗначенияПараметровДляТочкиМаршрута());
	ДополнитьСтруктуру(Значения, Ответ.ЗначенияПараметровДляТочкиМаршрута());
	ДополнитьСтруктуру(Значения, Сессия.ЗначенияПараметровДляТочкиМаршрута());
	Возврат Значения;
КонецФункции

Процедура ДополнитьСтруктуру(СтруктураПриемник, СтруктураИсточник)
	Для Каждого КиЗ Из СтруктураИсточник Цикл
		СтруктураПриемник.Вставить(КиЗ.Ключ, КиЗ.Значение);
	КонецЦикла;
КонецПроцедуры