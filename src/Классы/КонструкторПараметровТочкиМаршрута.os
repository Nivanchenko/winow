
&Желудь
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Функция СформироватьПараметры(ПараметрыМаршрута, ВходящийЗапрос, Ответ, Сессия) Экспорт

	Результат = Новый Массив();

	ЗначенияПараметров = СтруктураЗначенийПараметровДляТочкиМаршрута(ВходящийЗапрос, Ответ, Сессия);

	Для Каждого ПараметрМаршрута из ПараметрыМаршрута Цикл
		Значение = Неопределено;
		Если Не ЗначенияПараметров.Свойство(ПараметрМаршрута, Значение) Тогда
			ВызватьИсключение СтрШаблон("Неизвестный параметр <%1> в точке маршрута", ПараметрМаршрута);
		КонецЕсли;
		Результат.Добавить(Значение);
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

Функция СтруктураЗначенийПараметровДляТочкиМаршрута(ВходящийЗапрос, Ответ, Сессия)
	Значения = Новый Структура();	
	ДополнитьСтруктуру(Значения, ВходящийЗапрос.ЗначенияПараметровДляТочкиМаршрута());
	ДополнитьСтруктуру(Значения, Ответ.ЗначенияПараметровДляТочкиМаршрута());
	ДополнитьСтруктуру(Значения, Сессия.ЗначенияПараметровДляТочкиМаршрута());
	Возврат Значения;
КонецФункции

Процедура ДополнитьСтруктуру(СтруктураПриемник, СтруктураИсточник)
	Для Каждого КиЗ Из СтруктураИсточник Цикл
		СтруктураПриемник.Вставить(КиЗ.Ключ, КиЗ.Значение);
	КонецЦикла;
КонецПроцедуры