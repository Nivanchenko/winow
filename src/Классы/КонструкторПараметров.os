
&Желудь
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Функция СформироватьПараметрыТочкиМаршрута(Действие, ВходящийЗапрос, Ответ, Сессия) Экспорт

	ПараметрыМаршрута = Действие.Параметры;

	Результат = Новый Массив();

	ЗначенияПараметров = СтруктураЗначенийПараметровДляТочкиМаршрута(ВходящийЗапрос, Ответ, Сессия);

	Если Действие.УрлСодержитШаблон Тогда
		ПутьСПараметрами = СтрЗаменить(ВходящийЗапрос.Путь, Действие.АдресКонтроллера + "/", "");
		ДополнитьЗначениямиПараметровУРЛ(Действие.ПараметрыУРЛ, ПутьСПараметрами, ЗначенияПараметров);
	КонецЕсли;

	Для Каждого ПараметрМаршрута из ПараметрыМаршрута Цикл
		Значение = Неопределено;
		Если Не ЗначенияПараметров.Свойство(ПараметрМаршрута, Значение) Тогда
			ВызватьИсключение СтрШаблон("Неизвестный параметр <%1> в точке маршрута", ПараметрМаршрута);
		КонецЕсли;
		Результат.Добавить(Значение);
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

Процедура ДополнитьЗначениямиПараметровУРЛ(ПараметрыУРЛ, Путь, ЗначенияПараметров)
	ПутьВМассив = СтрРазделить(Путь, "/");

	Для каждого ТекПараметрУРЛ Из ПараметрыУРЛ Цикл

		Если ТекПараметрУРЛ.Ключ <= ПутьВМассив.ВГраница() Тогда
			ЗначенияПараметров.Вставить(ТекПараметрУРЛ.Значение, ПутьВМассив[ТекПараметрУРЛ.Ключ]);
		Иначе
			ЗначенияПараметров.Вставить(ТекПараметрУРЛ.Значение);
		КонецЕсли;

	КонецЦикла;
КонецПроцедуры

Функция СтруктураЗначенийПараметровДляТочкиМаршрута(ВходящийЗапрос, Ответ, Сессия)
	Значения = Новый Структура();	
	ДополнитьСтруктуру(Значения, ВходящийЗапрос.ЗначенияПараметровДляТочкиМаршрута());
	ДополнитьСтруктуру(Значения, Ответ.ЗначенияПараметровДляТочкиМаршрута());
	ДополнитьСтруктуру(Значения, Сессия.ЗначенияПараметровДляТочкиМаршрута());
	ДополнитьСтруктуру(Значения, ВходящийЗапрос.ПараметрыИменные);
	Возврат Значения;
КонецФункции

Процедура ДополнитьСтруктуру(СтруктураПриемник, СтруктураИсточник)
	Для Каждого КиЗ Из СтруктураИсточник Цикл
		СтруктураПриемник.Вставить(КиЗ.Ключ, КиЗ.Значение);
	КонецЦикла;
КонецПроцедуры