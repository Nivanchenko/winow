#Использовать autumn

&Пластилин
Перем Маршрутизатор Экспорт;

&Пластилин
Перем МенеджерОтображений Экспорт;

&Пластилин
Перем МенеджерДоступа Экспорт;

&Желудь
Процедура ПриСозданииОбъекта()

КонецПроцедуры

Процедура НайтиИЗарегистрироватьМаршруты(Желудь) Экспорт

	РефлекторОбъекта = РефлекторЖелудя(Желудь);

	МетодыМаршрут = РефлекторОбъекта.ПолучитьТаблицуМетодов("Маршрут", Ложь);

	Если МетодыМаршрут.Количество() > 1 Тогда

		ВызватьИсключение "Не может быть больше одного определения контроллера в классе " + Желудь;

	ИначеЕсли МетодыМаршрут.Количество() = 0 Тогда

		Возврат;

	КонецЕсли;

	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(МетодыМаршрут[0], "Маршрут");

	АдресКонтроллера = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);

	МетодыТочекМаршрутов = РефлекторОбъекта.ПолучитьТаблицуМетодов("ТочкаМаршрута");

	Если МетодыТочекМаршрутов.Количество() = 0 Тогда
		ВызватьИсключение "Не определено ниодной точки маршрута в контроллере " + Желудь;
	КонецЕсли;

	Для Каждого ТочкаМаршрута из МетодыТочекМаршрутов Цикл
		ЗарегистрироватьМаршрутКОбъекту(АдресКонтроллера, ТочкаМаршрута, Желудь);
	КонецЦикла;

КонецПроцедуры

Функция НайтиИУдалитьПараметрыУРЛ(Урл)
	Урл = СтрЗаменить(Урл, "\", "/");
	МассивВсехПараметров = СтрРазделить(Урл, "/");
	ОчищенныйУРЛ = Новый Массив;
	ПараметрыУРЛ = Новый Массив;
	ДошелДоШаблонаПараметра = Ложь;
	Для каждого ТекущийПараметр Из МассивВсехПараметров Цикл
		Если Не ЭтоШаблонПараметраУрл(ТекущийПараметр) и Не ДошелДоШаблонаПараметра Тогда
			ОчищенныйУРЛ.Добавить(ТекущийПараметр);
		
		ИначеЕсли Не ЭтоШаблонПараметраУрл(ТекущийПараметр) и ДошелДоШаблонаПараметра Тогда
			ВызватьИсключение "Шаблонные параметры url дложны быть в конце пути точки маршрута";
		
		ИначеЕсли ЭтоШаблонПараметраУрл(ТекущийПараметр) Тогда
			ПараметрыУРЛ.Добавить(Сред(ТекущийПараметр,2, СтрДлина(ТекущийПараметр) - 2)); 
			ДошелДоШаблонаПараметра = Истина;

		Иначе
			// Ничего

		КонецЕсли
	КонецЦикла;
	Урл = СтрСоединить(ОчищенныйУРЛ, "/");
	Возврат ПараметрыУРЛ;
КонецФункции

Функция ЭтоШаблонПараметраУрл(ЧастьУрла)
	Возврат СтрНачинаетсяС(ЧастьУрла, "{") И СтрЗаканчиваетсяНа(ЧастьУрла, "}");
КонецФункции

Процедура ЗарегистрироватьМаршрутКОбъекту(АдресКонтроллера, ТочкаМаршрута, Желудь)

	Действие = Новый Структура("Желудь, ИмяМетода, Параметры", Желудь, ТочкаМаршрута.Имя, ТочкаМаршрута.Параметры.ВыгрузитьКолонку("Имя"));
	
	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(ТочкаМаршрута, "ТочкаМаршрута");
	ИмяТочкиМаршута = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);

	Если СтрНайти(ИмяТочкиМаршута, "{") > 0 
			ИЛИ СтрНайти(ИмяТочкиМаршута, "}") > 0 Тогда
			Действие.Вставить("ПараметрыУРЛ", НайтиИУдалитьПараметрыУРЛ(ИмяТочкиМаршута));
	КонецЕсли;

	ПутьКМетоду = СтрЗаменить(ОбъединитьПути(АдресКонтроллера, ИмяТочкиМаршута), "\", "/");

	Маршрутизатор.ДобавитьМаршрут(ПутьКМетоду, Действие);

	ЗарегистрироватьОтображение(ТочкаМаршрута, Действие);

	ЗарегистрироватьРолиТочкиМаршрута(ТочкаМаршрута, Действие);

КонецПроцедуры

Процедура ЗарегистрироватьОтображение(ТочкаМаршрута, Действие)
	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(ТочкаМаршрута, "Отображение");
	Если НЕ Аннотация = Неопределено Тогда
		РасположениеОтображения = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);
		МенеджерОтображений.ДобавитьОтображениеДействия(Действие, РасположениеОтображения);
	КонецЕсли;	
КонецПроцедуры

Процедура ЗарегистрироватьРолиТочкиМаршрута(ТочкаМаршрута, Действие)
	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(ТочкаМаршрута, "Роли");
	Если НЕ Аннотация = Неопределено Тогда
		СписокРолей = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);
		МенеджерДоступа.ДобавитьРолиТочкиДоступаСписком(Действие, СписокРолей);
	КонецЕсли;	
КонецПроцедуры

Функция РефлекторЖелудя(Желудь)
	Возврат Новый РефлекторОбъекта(ТипЗнч(Желудь));
КонецФункции