#Использовать json

&Пластилин
Перем Лог Экспорт;

Перем Порт Экспорт;
Перем ИмяХоста Экспорт;
Перем ЗапросВФоновыхЗаданиях Экспорт;
Перем КаталогиСФайлами Экспорт;
Перем КаталогиСПриложениями Экспорт;


&Желудь
Процедура ПриСозданииОбъекта()
	Порт = 3333;
	ИмяХоста = "localhost";
	ЗапросВФоновыхЗаданиях = Истина;
	КаталогиСФайлами = Новый Соответствие();
	КаталогиСПриложениями = Новый Соответствие();
КонецПроцедуры

Процедура ПрочитатьИзФайла(ПутьКФайлу) Экспорт

	НастройкиТекстом = СодержимоеФайла(ПутьКФайлу);

	Если Не ЗначениеЗаполнено(НастройкиТекстом) Тогда
		Лог.Отладка("Не удалось прочитать настройки текстом");
		ВызватьИсключение "Файл настроек не существует, или пуст.";
	КонецЕсли;
	
	Настройки = РаспарситьНастройки(НастройкиТекстом);

	Порт = Настройки["Порт"];
	Лог.Отладка("В файле настроек найден параметр ""Порт"": %1", Порт);
	ИмяХоста = Настройки["ИмяХоста"];
	Лог.Отладка("В файле настроек найден параметр ""ИмяХоста"": %1", ИмяХоста);
	ЗапросВФоновыхЗаданиях = Настройки["ЗапросВФоновыхЗаданиях"];
	Лог.Отладка("В файле настроек найден параметр ""ЗапросВФоновыхЗаданиях"": %1", ЗапросВФоновыхЗаданиях);

	Если ТипЗнч(Настройки["КаталогиСФайлами"]) = Тип("Соответствие") Тогда
		Для каждого ФайлыИзНастроек Из Настройки["КаталогиСФайлами"] Цикл
			Лог.Отладка("В файле настроек найдено описание каталога с файлами. Ключ: %1, Значение: %2", ФайлыИзНастроек.Ключ, ФайлыИзНастроек.Значение);
			КаталогиСФайлами.Вставить(ФайлыИзНастроек.Ключ, ФайлыИзНастроек.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Для каждого ПриложенияИзНастроек Из Настройки["КаталогиСПриложениями"] Цикл
		Лог.Отладка("В файле настроек найдено описание каталога с приложением. Ключ: %1, Значение: %2", ФайлыИзНастроек.Ключ, ФайлыИзНастроек.Значение);
		КаталогиСПриложениями.Вставить(ПриложенияИзНастроек.Ключ, ПриложенияИзНастроек.Значение);
	КонецЦикла;

КонецПроцедуры

Процедура СгенерироватьФайлНастроек(ПутьДоНастроек) Экспорт
	Лог.Отладка("Приступаю к генерации файла настроек. Путь до файла: %1", ПутьДоНастроек);

	СоотвПриложения = Новый Соответствие();
	СоотвПриложения.Вставить("Приложение", "./app/scripts");

	СоотвФайлы = Новый Соответствие();
	СоотвФайлы.Вставить("/files", "./app/files");

	Настройки = Новый Структура();
	Настройки.Вставить("Порт", Порт);
	Лог.Отладка("В файл настроек записан параметр ""Порт"": %1", Порт);
	Настройки.Вставить("ИмяХоста", ИмяХоста);
	Лог.Отладка("В файл настроек записан параметр ""ИмяХоста"": %1", ИмяХоста);
	Настройки.Вставить("ЗапросВФоновыхЗаданиях", ЗапросВФоновыхЗаданиях);
	Лог.Отладка("В файл настроек записан параметр ""ЗапросВФоновыхЗаданиях"": %1", ЗапросВФоновыхЗаданиях);
	Настройки.Вставить("КаталогиСПриложениями", СоотвПриложения);
	Настройки.Вставить("КаталогиСФайлами", СоотвФайлы);

	Парсер = Новый ПарсерJSON();
	НастройкиТекстом = Парсер.ЗаписатьJSON(Настройки);

	ЗаписьТекста = Новый ЗаписьТекста();
	ЗаписьТекста.Открыть(ПутьДоНастроек);
	ЗаписьТекста.Записать(НастройкиТекстом);
	ЗаписьТекста.Закрыть();
	Лог.Отладка("Файл настроек успешно записан на диск");

КонецПроцедуры

Функция РаспарситьНастройки(НастройкиТекстом)
	
	Парсер = Новый ПарсерJSON();
	Возврат Парсер.ПрочитатьJSON(НастройкиТекстом);

КонецФункции

Функция СодержимоеФайла(ПутьКФайлу)
	Лог.Отладка("Начинаю чтение настроек из файла: %1", ПутьКФайлу);
	Если Новый Файл(ПутьКФайлу).Существует() Тогда
		Лог.Отладка("Файл настроек найден");
		Чтение = Новый ЧтениеТекста();
		Чтение.Открыть(ПутьКФайлу);
		Текст = Чтение.Прочитать();
		Чтение.Закрыть();
		Лог.Отладка("Из файла прочитаны настройки: %1", Текст);
		Возврат Текст;

	Иначе
		Лог.Отладка("Файл настроек не существует!");
		Возврат "";
	КонецЕсли;

КонецФункции




